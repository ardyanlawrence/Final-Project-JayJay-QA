plugins {
    id 'java'
}

group = 'org.ardyan'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'io.cucumber:cucumber-junit:7.30.0'
    testImplementation 'io.cucumber:cucumber-java:7.30.0'
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.38.0'
    testImplementation 'io.github.bonigarcia:webdrivermanager:6.3.2'
    testImplementation 'org.testng:testng:7.11.0'
    testImplementation 'org.json:json:20250517'
    testImplementation 'io.rest-assured:json-schema-validator:5.5.0'
    testImplementation 'io.rest-assured:rest-assured:5.5.0'
}

test {
    useJUnit()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

def optionalTag = project.hasProperty('tags') ? project.getProperty('tags') : ''
def finalApiTag = optionalTag ? "(@api) and (${optionalTag})" : "@api"
tasks.register('api', JavaExec) {
    group = 'verification'
    description = 'Run API tests with optional tag filter'
    dependsOn 'testClasses'

    classpath = sourceSets.test.runtimeClasspath
    mainClass.set('io.cucumber.core.cli.Main')

    args = [
            '--plugin', 'pretty',
            '--plugin', 'html:reports/api_reports.html',
            '--plugin', 'json:reports/api_reports.json',
            '--glue', 'org.ardyan.stepdef',
            '--tags', finalApiTag,
            'src/test/resources'
    ]
}

def finalWebTag = optionalTag ? "(@web) and (${optionalTag})" : "@web"
tasks.register('web', JavaExec) {
    group = 'verification'
    description = 'Run API tests with optional tag filter'
    dependsOn 'testClasses'

    classpath = sourceSets.test.runtimeClasspath
    mainClass.set('io.cucumber.core.cli.Main')

    args = [
            '--plugin', 'pretty',
            '--plugin', 'html:reports/web_reports.html',
            '--plugin', 'json:reports/web_reports.json',
            '--glue', 'org.ardyan.stepdef',
            '--tags', finalWebTag,
            'src/test/resources'
    ]
}